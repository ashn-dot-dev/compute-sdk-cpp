default: build

build-dir := "target"
wasi-sdk := "/opt/wasi-sdk"
default-example := "echo"

# Both are here because `target` might still be generated by e.g. rust-analyzer
# externally, even if `{{build-dir}}` changes
clean:
    rm -rf .cache {{build-dir}} target
    
example name=default-example: (build-example name)
    fastly compute serve -C examples --file ../{{build-dir}}/examples/{{name}}.wasm

strip-example name=default-example: (build-example name)
    {{wasi-sdk}}/bin/strip ./{{build-dir}}/examples/{{name}}.wasm
    
build-example name=default-example: (cmake-example name)
    cmake --build {{build-dir}}/examples
    
cmake-example name=default-example:
    cmake -S ./examples -B {{build-dir}}/examples -DWASI_SDK={{wasi-sdk}} -DEXAMPLE_NAME={{name}} -DENABLE_LTO=ON -DCMAKE_BUILD_TYPE=Release -DCMAKE_TOOLCHAIN_FILE={{wasi-sdk}}/share/cmake/wasi-sdk-p1.cmake -DCMAKE_EXPORT_COMPILE_COMMANDS=1 
    
build: cmake
    cmake --build {{build-dir}}

cmake:
    cmake -S . -B {{build-dir}} -DWASI_SDK={{wasi-sdk}} -DENABLE_LTO=ON -DCMAKE_BUILD_TYPE=Release -DCMAKE_TOOLCHAIN_FILE={{wasi-sdk}}/share/cmake/wasi-sdk-p1.cmake -DCMAKE_EXPORT_COMPILE_COMMANDS=1 
